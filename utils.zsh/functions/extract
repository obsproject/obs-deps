autoload -Uz log_debug log_error log_status

if (( # < 1 )) {
  log_error 'Called without enough arguments.'
  return 2
}

log_debug "Attempt to detect and extract archive ${1}"

local _tarflags='xzf'
local _vflag=('' '-v')
if (( _loglevel > 1 )) _tarflags="v${_tarflags}"

while (( # > 0 )) {
  case "${(L)1}" in
    *.tar.gz) tar "${_tarflags}" "${1}" ;;
    *.tar.xz)
      if tar -J --help &> /dev/null; then
        XZ_OPT=-T0 tar "-${_tarflags//z/J}" "${1}"
      else
        xzcat "${1}" | tar -xf -
      fi
      ;;
    *.tar.bz|*.tar.bz2|*.tbz|*.tbz2) tar ${_tarflags//z/j} ${1} ;;      
    *.tar) tar "-${_tarflags}" "${1}" ;;
    *.zip) unzip "${_vflag[(( _loglevel > 1 ))]}" "${1}" ;;
    *.lzma) unlzma -T0 "${_vflag[(( _loglevel > 1 ))]}" "${1}" ;;
    *.xz) unxz -T0 "${_vflag[(( _loglevel > 1 ))]}" "${1}" ;;
    *.7z) 7za x "${1}" ;;
    *) log_error "Unknown archive format '${1}'" ;;
  esac
  shift
}

